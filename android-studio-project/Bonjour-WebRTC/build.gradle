apply from: '../constants.gradle'
apply plugin: 'com.android.application'

android {
    compileSdkVersion project.ext.compileSdkVersion
    buildToolsVersion project.ext.buildToolsVersion

    compileOptions {
        sourceCompatibility project.ext.javaVersion
        targetCompatibility project.ext.javaVersion
    }

    defaultConfig {
        minSdkVersion project.ext.minSdkVersion
        targetSdkVersion project.ext.targetSdkVersion

        applicationId "com.github.warren_bank.bonjour_webrtc"
        versionName project.ext.releaseVersion
        versionCode project.ext.releaseVersionCode

        archivesBaseName += "-$versionName"
    }

    flavorDimensions "languageResources"

    productFlavors {
        withAllLanguageTranslations {
            dimension "languageResources"
        }
        english {
            dimension "languageResources"
        }
    }

    buildTypes {
        release {
            debuggable      false
            jniDebuggable   false
            minifyEnabled   true
            shrinkResources true
            proguardFiles = [
                "proguard-rules.txt",
                getDefaultProguardFile('proguard-android.txt')
            ]
        }
        debug {
            debuggable      true
            jniDebuggable   true
            minifyEnabled   false
            shrinkResources false
        }
    }

    splits {
        abi {
            enable true
            reset()
            include "arm64-v8a", "armeabi-v7a", "x86", "x86_64"
            universalApk false
        }
    }

    lintOptions {
        disable 'MissingTranslation'
        abortOnError true
    }
}

dependencies {
    implementation      'org.jmdns:jmdns:'                         + project.ext.libVersionJmDNS       // (209.0 KB) https://mvnrepository.com/artifact/org.jmdns/jmdns
    implementation      'org.webrtc:google-webrtc:'                + project.ext.libVersionWebRTC      // ( 26.7 MB) https://mvnrepository.com/artifact/org.webrtc/google-webrtc

    compileOnly         'com.android.support:support-annotations:' + project.ext.libVersionSupport     // ( 22.0 KB) https://mvnrepository.com/artifact/com.android.support/support-annotations?repo=google
    annotationProcessor 'com.android.support:support-annotations:' + project.ext.libVersionSupport
}
